#! /usr/bin/perl -w
# by pts@fazekas.hu at Thu Dec 12 23:38:13 CET 2013
#
# TODO(pts): bug: unexpected reloc type in static binarySegmentation fault (core dumped)
#            .../bin/clang++ -static -m64 -s -O2 -fno-exceptions ~/prg/hello.cc
#            (doesn't happen if `-static' without `-s', and we strip later)
#            doesn't happen on Ubuntu Lucid (32-bit); happens on Ubuntu Precise
#            (64-bit)
#            Try upgrading ld from: GNU ld (GNU Binutils for Ubuntu) 2.20.1-system.20100303
#            to: GNU ld (GNU Binutils for Ubuntu) 2.22
# TODO(pts): !! clang-xstatic uses its own ld now, how if not in bin?
#            (would find it in bin) bin would be searched first, no on $PATH
# TODO(pts): compile a smaller linker (maybe no cross-compilation support?)
# TODO(pts): use ld.bin in bin
# TODO(pts): do `clang -m64' by default on 64-bit? `gcc -v' prints
#            `Target: x86_64-linux-gnu', but can we detect it? Detect /bin/sh
#            as a 64-bit ELF and `uname -m' or `uname -p' (unknown in busybox)
#            as x86_64?
#            This has to be implemented in clang_noxstatic.c.
# TODO(pts): Add gnu/stubs-64.h for 64-bit compilation on narancs.
# TODO(pts): Make `ld' Perl script just a symlink.
# TODO(pts): Add pathbin directory with symlinks, document i
# TODO(pts): Add magic Perl header.
# TODO(pts): Document new conditions in usr/include/bits/cpp_type_traits.h
#            and usr/include/limits
# TODO(pts): Rewrite ld Perl script in C once we have Clang support.
# TODO(pts): Rewrite in C once we have Clang support.
# TODO(pts): Test exception handling in various g++ and Clang versions.
#
# Tested and found working with GCC (both gcc and g++) versions: 4.4.3 on
# Ubuntu Lucid, 4.6.2 on Ubuntu Precise and
# 4.8.2 on Debian Jessie.
#
# The error
#
#   ....o:(.eh_frame+0x...): undefined reference to `__gxx_personality_v0'
#
# indicates that cc1plus and libstdc++ were compiled with a different exception
# handling (EH) personality. The other one is __gxx_personality_lj0. There is
# no easy fix except for recompiling one of them. (Sometimes hello.cc compiles
# without -fno-exceptions, but other code doesn't.) If exception handling is not
# needed, then `-fno-exceptions' can be specified at .cc compilation time, and
# the linker error will go away.
#

use integer;
use strict;

sub run_xstatic() {
  die "Usage: $0 <gcc|g++> [<gcc-arg>...]
Invokes gcc -static against the xstatic uClibc.
" if !@ARGV or $ARGV[0] eq '--help';
  die "$0: error: please specify gcc prog in 1st arg\n" if $ARGV[0] =~ /\A-/;

  my $gcc = shift(@ARGV);
  #my $argv0 = readlink("/proc/self/exe");  # /usr/bin/perl, useless.
  my $prog0 = index($0, '/') < 0 ? "./$0" : $0;  # TODO(pts): $ENV{PATH} lookup.
  my $link0 = readlink($prog0);
  #die "$0: error: cannot readlink /proc/self/exe: $!\n" if !defined $link0;
  $prog0 = substr($link0, 0, 1) eq '/' ? $link0 :
      substr($prog0, 0, rindex($prog0, '/') + 1) . $link0 if defined $link0;
  die "$0: error: prog not in bin: $prog0\n" if
      $prog0 !~ m@(/bin/[^/]++)\Z(?!\n)@;
  my $sysroot = substr($prog0, 0, -length($1));
  {
    my $crt1_fn = "$sysroot/gccld/crt1.o";  # Other *crt*.o is also needed.
    die "$0: error: file not found: $crt1_fn\n" if !(-f $crt1_fn);
    my $ld_fn = "$sysroot/gccld/crt1.o";  # Other *crt*.o is also needed.
    die "$0: error: file not found: $ld_fn\n" if !(-f $ld_fn);
  }
  my $gcc_prog = $gcc;
  $gcc_prog =~ s@\A.*/@@s;
  my $is_cxx = index($gcc_prog, '++') >= 0 ? 1 : 0;
  my $is_clang = index($gcc_prog, 'clang') >= 0 ? 1 : 0;
  my $has_nostdincxx = 0;
  my $has_explicit_target = 0;
  my $i = 0;
  for ($i = 0; $i < @ARGV; ++$i) {
    my $arg = $ARGV[$i];
    if ($arg eq '-xc++') {
      $is_cxx = 1;
    } elsif ($arg eq '-ccc-cxx') {  # Enable C++ for Clang.
      # -ccc-cxx is a Clang-specific flag, gcc doesn't have it.
      $is_cxx = 1;
      $is_clang = 1;
    } elsif ($arg eq '-x' and $i < $#ARGV) {
      $is_cxx = 1 if $ARGV[++$i] eq 'c++';
    } elsif ($arg eq '-nostdinc++') {
      $has_nostdincxx = 1;
    } elsif ($arg eq '-target' and $i < $#ARGV) {  # Clang-specific.
      $has_explicit_target = 1;
      ++$i;
    } elsif ($arg eq '-m64') {
      # xstatic can create 32-bit binaries only.
      die "$0: error: flag not supported in xstatic: $arg\n";
    } elsif ($arg eq '-shared' or $arg eq '-shared-libgcc') {
      # xstatic cannot generate shared libraries.
      die "$0: error: flag not supported in xstatic: $arg\n";
    } elsif ($arg =~ /\A-mcpu=/ or $arg =~ /\A-march=/ or $arg eq '-m32') {
      $has_explicit_target = 1;
    } elsif ($arg =~ /\A-B/) {
      die "$0: error: flag not supported in xstatic: -B\n";
    } elsif ($arg =~ /\A--(sysroot|gcc-toolchain)(?![^=])/) {
      # gcc-toolchain specifies too much (include, lib, tool etc.).
      die "$0: error: flag not supported in xstatic: --$1\n";
    } elsif ($arg !~ /\A-/ and
             $arg =~ /[.](?:cc|cp|cxx|cpp|CPP|c[+][+]|C)\Z(?!\n)/) {
      $is_cxx = 1;
    }
  }
  my $has_v = (grep { $_ eq '-v' } @ARGV) ? 1 : 0;
  my $need_linker = (grep { $_ eq '-E' or $_ eq '-S' or $_ eq '-c' or
      $_ eq '-M' or $_ eq 'MM' } @ARGV) ? 0 : 1;

  # Our $sysroot/gccld/ld binary would be invoked because of -B. And that binary
  # would run us.
  #
  # In addition to -B, the gccld/i386-linux-gnu symlink is needed on Ubuntu
  # Precise so that the right crt1.o gets found.
  #
  # TODO(pts): Detect wrong crt1.o (in /usr/lib/i386-linux-gnu etc.).
  my @a = (
      $gcc, '-static',
      # In Clang, -m32 has higher priority than `-target'. So don't specify
      # ours if there is one already specified.
      $has_explicit_target ? () : ('-m32'),
      # Without this we get the following error compiling binutils 2.20.1:
      # chew.c:(.text+0x233f): undefined reference to `__stack_chk_fail'
      # We can't implement this in a compatible way, glibc gcc generates %gs:20,
      # uClibc-0.9.33 has symbol __stack_chk_guard.
      '-fno-stack-protector',
      $need_linker ? (  # `clang -c' warns about these flags.
        '-Wl,--xstatic-ld',
        '-Wl,' . $prog0,
        '-Wl,' . $has_v,
        '-Wl,' . $sysroot) : (),
      $is_cxx && !$has_nostdincxx ? (
          # Without -nostdinc++, /usr/include/c++/4.4/iostream would take
          # precedence over $sysroot/usr/include/iostream.
          '-nostdinc++',
          # gcc doesn't have the -cxx-isystem flag, so `#include <vector>' would
          # be found even if `gcc foo.c' includes it.
          ($is_clang ? '-cxx-isystem' : '-isystem'),
          "$sysroot/usr/c++include",
      ) : (),
      '--sysroot', $sysroot, "-B$sysroot/gccld", @ARGV);
  # TODO(pts): Be smarter to detect no input files.
  @a = ($gcc, '-m32', '-static', @ARGV) if !@ARGV or ($ARGV[0] eq '-v');
  die "$0: error: exec $a[0]: $!\n" if !exec @a;
}

sub run_ld() {
  my $is_static;
  my $i;
  my $gccbdir;
  my $sysroot;
  my $sysroot_arg;
  my $is_verbose = 0;
  for ($i = 0; $i < @ARGV; ++$i) {
    my $arg = $ARGV[$i];
    if ($arg eq '-static') {
      $is_static = 1;
    } elsif ($arg eq '--sysroot' and $i < $#ARGV) {
      die "$0: error: multiple --sysroot args\n" if defined $sysroot_arg;
      $sysroot_arg = $ARGV[++$i];
    } elsif ($arg =~ /\A--sysroot=(.*+)/s) {
      die "$0: error: multiple --sysroot args\n" if defined $sysroot_arg;
      $sysroot_arg = $1;
    } elsif ($arg eq '--do-xstatic-ld' and $i + 2 < @ARGV) {
      $is_verbose = $ARGV[++$i];
      $sysroot = $ARGV[++$i];
    # /usr/lib/gcc/i486-linux-gnu/4.4.3/crtbeginT.o
    # There is crtbeginT.o, crtbeginS.o and crtbegin.o. When building static
    # executables, crtbeginT.o is used.
    } elsif (!defined $gccbdir and $arg =~ m@(/crtbeginT?[.]o)\Z(?!\n)@) {
      $gccbdir = substr($arg, 0, -length($1));
    }
  }
  die "$0: error: missing --do-xstatic-ld\n" if !defined($sysroot);
  die "$0: error: missing -static\n" if !$is_static;
  die "$0: error: sysroot mismatch: do=($sysroot) arg=($sysroot_arg)\n" if
      defined($sysroot_arg) and $sysroot_arg ne $sysroot;

  my $lgccld = "-L$sysroot/gccld";
  my $lgccldi = "-L$sysroot/gccld/i386-linux-gnu";
  if (!defined $gccbdir) {  # Probably `gcc -nostdlib'.
    for ($i = 0; $i < @ARGV; ++$i) {
      my $arg = $ARGV[$i];
      if ($arg =~ /\A-L/ and $arg ne $lgccld) {
        # Pick the first -L which is not uClibc's ($lgccld).
        $gccbdir = substr($arg, 2);
        last;
      }
    }
    die "$0: error: cannot detect gcc built-in -B directory\n" if
        !defined($gccbdir);
  }

  # `ld -nostdlib' is different from `gcc -nostdlib'.
  # Move our ($sysroot) lib directory to the front, before $gccbdir (with
  # libgcc.a).
  my @a = ('ld', '-nostdlib', "-L$sysroot/usr/lib");
  my $lsysrootusrlib = "-L$sysroot/usr/lib";
  my $lsysrootusrlibddlib = "-L$sysroot/usr/lib/../lib";
  my $has_nodir_crt = 0;
  for ($i = 0; $i < @ARGV; ++$i) {
    my $arg = $ARGV[$i];
    if ($arg eq '--do-xstatic-ld' and $i + 2 < @ARGV) {  # A marker with arg.
      $i += 2;
    } elsif ($arg eq '--sysroot' and $i < $#ARGV) {
      ++$i;
    } elsif ($arg =~ /\A--sysroot=/) {
    } elsif (0 or
        $arg eq '--build-id' or  # Would increase file size.
        $arg eq '--hash-style=both' or  # Would increase file size.
        $arg eq '--hash-style=gnu' or  # Seems to be unneeded.
        $arg eq '-nostdlib' or  # Added above, no need to duplicate.
        $arg eq '-L/usr/lib' or  # Usually added by clang.
        $arg eq '-L/lib' or  # Usually added by clang.
        $arg eq '-L/usr/local/lib' or  # Just for simplicity.
        $arg eq $lgccld or
        $arg eq $lgccldi or
        $arg eq $lsysrootusrlib or
        $arg eq $lsysrootusrlibddlib or
        # -L/usr/lib/gcc/i486-linux-gnu/4.4.3/../../../../lib  # /usr/lib
        $arg =~ m@\A-L@ and $arg =~ m@/\.\./\.\./\.\./\.\./lib(?:32)?\Z(?!\n)@ or
        $arg =~ m@\A-L@ and $arg =~ m@/\.\./\.\./\.\.\Z(?!\n)@ or
        $arg =~ m@\A-L@ and $arg =~ m@/\.\./\.\./\.\./i386-linux-gnu\Z(?!\n)@ or
       0) {
    } elsif ($arg eq '-z' and $i < $#ARGV and $ARGV[$i + 1] eq 'relro') {
      # Would increase file size.
      ++$i;
    } elsif ($arg eq '-lstdc++') {
      # Fixup for linker errors: undefined reference to `_Unwind_SjLj_Unregister'.
      push @a, $arg, '-lstdc++usjlj';  # The order is important.
    } elsif ($arg eq 'crtbeginT.o') {  # For Clang.
      if (!$has_nodir_crt) {
        push @a, "-L$sysroot/clanglibgcc";
        $has_nodir_crt = 1;
      }
      push @a, "$sysroot/clanglibgcc/$arg";
    } elsif ($has_nodir_crt and
             ($arg eq 'crtend.o' or $arg eq 'crtfastmath.o')) {
      push @a, "$sysroot/clanglibgcc/$arg";
    } else {
      push @a, $arg;
    }
  }

  if (-f "$gccbdir/ld") {
    # If not found here we could try "$gccbdir/../../ld", i.e. /usr/lib/gcc/ld,
    # because `gcc -B/usr/lib/gcc' also tries it.
    $a[0] = "$gccbdir/ld";  # Override 'ld' (on $PATH).
  }

  print STDERR "$0: info: running linker:\n@a\n" if $is_verbose;
  die "$0: error: exec $a[0]: $!\n" if !exec @a;
}

# When gcc runs ld (as collect2), the -Wl,... flags are not passed at the
# front.
if (@ARGV and $ARGV[0] eq '--do-xstatic-ld') {
  run_ld();
} else {
  run_xstatic();
}
